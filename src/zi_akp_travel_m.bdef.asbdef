managed; //implementation in class zbp_i_akp_travel_m unique;
strict ( 2 );

define behavior for ZI_AKP_TRAVEL_M alias Travel
implementation in class zbp_i_akp_travel_m unique
persistent table zakp_travel_m
lock master
authorization master ( instance )
etag master LastChangedAt
early numbering
{
  // Semantic Key field, which is readonly for the consumer, value is assigned in early numbering
  field ( readonly ) TravelId;

  // administrative fields: read only
  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;

  // mandatory fields that are required to create a travel
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;

  // validations
  validation validateCustomer on save { create; field CustomerId; }
  validation validateAgency on save { create; field AgencyId; }
  validation validateStatus on save { create; field OverallStatus; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateDates on save { create; field BeginDate, EndDate; }  //on create automaticall applies on update as well

  // standard operations for travel entity
  create;
  update;
  delete;

  // create booking by association
  association _Booking { create ( features : instance ); }

  // instance factory action for copying travel instances
  factory action copyTravel [1];   //no need to define result for factory action,as it will return BO instance

  // instance action and dynamic action control
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  // internal action that is called by determinations
  internal action reCalcTotalPrice;

  determination calucateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  side effects
  {
    //When the user changes the value of BookingFee/CurrencyCode, the determination calucateTotalPrice is called and modifies the value of TotalPrice.
    //In order for the field TotalPrice to show the updated values without a refresh, side effects is required to request UI to reload the field.
    field BookingFee affects field TotalPrice;
    field CurrencyCode affects field TotalPrice;
  }

  mapping for zakp_travel_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZI_AKP_BOOOKING_M alias Booking
implementation in class zbp_i_akp_booking_m unique
persistent table zakp_boooking_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  // static field control
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( readonly ) TravelId, BookingId;
  // Fields that are mandatory for create but should be read-only afterwards
  field ( mandatory : create, readonly : update ) BookingDate, CustomerId;

  update;
  delete;
  association _Travel;
  association _BookingSupplement { create ( features : instance ); }

  // validations
  validation validateStatus on save { create; field BookingStatus; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }

  determination calculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  side effects
  {
    //Changing the Booking status and on save the create action for booking supplement..
    //..is enabled/disabled automatically, without needing to refresh the page.
    field BookingStatus affects permissions ( create _BookingSupplement );
    //When the field FlightPrice/CurrencyCode in Child entity is modified, this will trigger the determination calculateTotalPrice.
    //Side effects will trigger the refresh of field TotalPrice from the Root entity to show the calculated total every time user enters a value at the child instance.
    field FlightPrice affects field _Travel.TotalPrice;
    field CurrencyCode affects field _Travel.TotalPrice;
  }


  mapping for zakp_boooking_m
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZI_AKP_BOOKSUPPL_M alias BookingSuppl
implementation in class zbp_i_akp_bookingSuppl_m unique
persistent table zakp_booksuppl_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  // static field control
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) Price, SupplementId, CurrencyCode;
  association _Travel;
  association _Booking;

  determination calculateTotalPrice on modify { create; field Price, CurrencyCode; }
  side effects
  {
    field Price affects field _Travel.TotalPrice;
    field CurrencyCode affects field _Travel.TotalPrice;
  }
  mapping for zakp_booksuppl_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}